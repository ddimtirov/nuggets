/*
 *    Copyright 2016 by Dimitar Dimitrov
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

tasks.withType(Jar) {
    from file('LICENSE')
    duplicatesStrategy = 'EXCLUDE'
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                Node root = asNode()

                root.appendNode('name', project.name)
                root.appendNode('description', project.description)
                root.appendNode('url', "https://github.com/ddimtirov/${project.name}")

                def issues = root.appendNode( 'issueManagement' )
                issues.appendNode( 'system', 'github' )
                issues.appendNode( 'url', "https://github.com/ddimtirov/${project.name}/issues" )

                def scm = root.appendNode( 'scm' )
                scm.appendNode( 'url', 'https://github.com/ddimtirov/${project.name}' )
                scm.appendNode( 'connection', "scm:git:https://github.com/ddimtirov/${project.name}.git")
                scm.appendNode( 'developerConnection', "scm:git:git@github.com:ddimtirov/${project.name}.git")

                def license = root.appendNode( 'licenses' ).appendNode( 'license' );
                license.appendNode( 'name', 'The Apache Software License, Version 2.0' )
                license.appendNode( 'url', 'http://www.apache.org/licenses/LICENSE-2.0.txt' )
                license.appendNode( 'distribution', 'repo' )

                def dev = root.appendNode( 'developers' ).appendNode( 'developer' );
                dev.appendNode( 'name', 'Dimitar Dimitrov' )
                dev.appendNode( 'email', 'dimitar.dimitrov@gmail.com' )
            }
        }
    }
}