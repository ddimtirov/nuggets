/*
 *    Copyright 2016 by Dimitar Dimitrov
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

// handles distribution of snapshots to Artifactory (oss.jfrog.org)

artifactory {
    contextUrl = project.findProperty('artifactoryUrl') ?: 'http://oss.jfrog.org/artifactory'
    publish {
        repository {
            repoKey = project.version.endsWith('SNAPSHOT') ? 'oss-snapshot-local' : 'oss-release-local'
            username = bintrayUsername
            password = bintrayKey
        }
        defaults {
            publications ('mavenJava')
        }
    }
    resolve {
        repository { repoKey = 'libs-release' }
    }

//    assert clientConfig instanceof org.jfrog.build.extractor.clientConfiguration.ArtifactoryClientConfiguration
    if (System.getenv("CI")=="true") {
        clientConfig.includeEnvVars = true
        clientConfig.envVarsExcludePatterns = "*TOKEN*,*SECRET*,*PASS*,*KEY*"
        clientConfig.info.agentName = "Travis"
        clientConfig.info.agentVersion = System.getenv("TRAVIS_OS_NAME")
    }
    clientConfig.info.licenseControl.with {
        autoDiscover=true
        runChecks=true
        includePublishedArtifacts=true
    }
}

artifactoryPublish {
    onlyIf {
        def pullRequest = System.getenv('TRAVIS_PULL_REQUEST')
        return !pullRequest || pullRequest=='false'
    }
}